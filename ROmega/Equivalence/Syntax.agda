{-# OPTIONS --safe #-}
module ROmega.Equivalence.Syntax where

open import Agda.Primitive
open import Level

open import ROmega.Kinds
open import ROmega.Types
open import ROmega.Types.Substitution

--------------------------------------------------------------------------------
-- Generalized vars.

private
  variable
    ℓτ ℓΔ ℓκ ℓκ' ℓκ₁ ℓκ₂ ℓL ι : Level
    Δ : KEnv ℓΔ
    κ : Kind ℓκ
    κ' : Kind ℓκ'
    κ₁ : Kind ℓκ₁
    κ₂ : Kind ℓκ₂

--------------------------------------------------------------------------------
-- Type & Predicate equivalence.

data _≡p_ : (π₁ π₂ : Pred Δ κ) → Set
            
data _≡t_ : (τ υ : Type Δ κ) → Set

infix 0 _≡p_
infix 0 _≡t_

data _≡p_ where
  peq-≲ : ∀ {τ₁ τ₂ υ₁ υ₂ : Type Δ R[ κ ]} →

          τ₁ ≡t υ₁ → τ₂ ≡t υ₂ →
          ------------------------
          (τ₁ ≲ τ₂) ≡p υ₁ ≲ υ₂

  peq-· : ∀ {τ₁ τ₂ τ₃ υ₁ υ₂ υ₃ : Type Δ R[ κ ]} →

            τ₁ ≡t υ₁ → τ₂ ≡t υ₂ → τ₃ ≡t υ₃ →
            ----------------------------------
            τ₁ · τ₂ ~ τ₃ ≡p υ₁ · υ₂ ~ υ₃

data _≡t_ where
  teq-refl : {τ : Type Δ κ} →

             ---------------
             τ ≡t τ

  teq-sym : ∀ {τ₁ τ₂ : Type Δ κ} →
             
              τ₁ ≡t τ₂ →
              ---------------
              τ₂ ≡t τ₁            

  teq-trans : ∀ {τ₁ τ₂ τ₃ : Type Δ κ} →
             
                τ₁ ≡t τ₂ → τ₂ ≡t τ₃ →
                ----------------------
                τ₁ ≡t τ₃

  teq-⇒ : ∀ {τ₁ τ₂ : Type Δ (★ ℓτ)} {π₁ π₂ : Pred Δ κ} →
             
               π₁ ≡p π₂ → τ₁ ≡t τ₂ →
               -----------------------
               π₁ ⇒ τ₁ ≡t π₂ ⇒ τ₂

  teq-∀ : ∀ {τ υ : Type (Δ , κ) (★ ℓτ)} →

            τ ≡t υ →
            ----------------
            `∀ κ τ ≡t `∀ κ υ

  teq-β     : ∀ {τ : Type (Δ , κ) κ'} {υ : Type Δ κ} →
                
                ------------------------------
                ((`λ κ τ) ·[ υ ]) ≡t (τ β[ υ ])

  teq-· : ∀ {τ₁ υ₁ : Type Δ (κ `→ κ')} {τ₂ υ₂ : Type Δ κ} → 

           τ₁ ≡t υ₁ → τ₂ ≡t υ₂ → 
           ------------------------
           τ₁ ·[ τ₂ ] ≡t υ₁ ·[ υ₂ ]

  teq-sing : ∀  {l₁ l₂ : Type Δ (L {ℓL})} →
                {τ₁ τ₂ : Type Δ κ₁} →
                
                l₁ ≡t l₂ → τ₁ ≡t τ₂ →
                --------------------------------------
                (l₁ R▹ τ₁) ≡t (l₂ R▹ τ₂)

  teq-lift₁ : ∀ {l : Type Δ (L {ℓL})} {υ : Type Δ κ} {τ : Type Δ (κ `→ κ')} →
                
                --------------------------------------
                (l R▹ τ) ·⌈ υ ⌉ ≡t (l R▹ (τ ·[ υ ]))


  teq-lift₂ : ∀ {l : Type Δ (L {ℓL})} {υ : Type Δ (κ₁ `→ κ₂)} {τ : Type Δ κ₁} →
                
                --------------------------------------
                ⌈ υ ⌉· (l R▹ τ) ≡t (l R▹ (υ ·[ τ ]))

  teq-⌊⌋    : ∀ {τ υ : Type Δ (L {ℓL})} →

                τ ≡t υ → 
                -------------
                ⌊_⌋ {ι = ι} τ ≡t ⌊_⌋ {ι = ι} υ

  teq-Π : ∀ {ρ₁ ρ₂ : Type Δ R[ ★ ℓκ ] } →
          ρ₁ ≡t ρ₂ → 
          -------------
          Π ρ₁ ≡t Π ρ₂

  teq-Σ : ∀ {ρ₁ ρ₂ : Type Δ R[ ★ ℓκ ] } →
          ρ₁ ≡t ρ₂ → 
          -------------
          Σ ρ₁ ≡t Σ ρ₂


