module Rome.Syntax where

open import Preludes.Level
open import Preludes.Data
open import Preludes.Relation

open import Rome.Kinds
open import Rome.GVars.Levels


--------------------------------------------------------------------------------
-- infix OOP.

infixr 9 _`→_
infixr 9 _⇒_
infixr 10 _▹_
infixr 10 _R▹_
infixr 10 _≲_
infix 10 _·_~_
infixl 11 _·[_]

infix 12 ↑_ _↑

--------------------------------------------------------------------------------
-- Labels are Strings.

Label : Set
Label = String

--------------------------------------------------------------------------------
-- Types and type vars.

data Env : Level → Set 
data Pred : Env ℓ → (κ : Kind ι) → Set 
data Type : (Δ : Env ℓ) → Kind ι →  Set


data Ent  : {κ : Kind ℓ} → (Δ : Env ℓ) → Pred Δ κ → Set
data _≡p_ : {κ : Kind ℓ} {Δ : Env ℓ} → (π₁ π₂ : Pred Δ κ) → Set
data _≡t_ : ∀ {κ : Kind ℓ} {Δ₁ : Env ℓ₁}{Δ₂ : Env ℓ₂} → (τ : Type Δ₁ κ) → (υ : Type Δ₂ κ) → Set

infix 0 _≡p_
infix 0 _≡t_

data Env where
  ε   : Env ℓ
  _،_ : Env ℓΔ → (Kind ℓκ) → Env (ℓΔ ⊔ ℓκ)
  _؛_ : {κ : Kind ℓκ} → (Δ : Env ℓΔ) → Pred Δ κ → Env (ℓΔ ⊔ ℓκ)

data TVar : Env ℓ → Kind ι → Set
data PVar : {κ : Kind ℓκ} → (Δ : Env ℓ) → Pred Δ κ → Set

_β[_] : ∀ {ℓΔ ℓκ ℓι} {Δ : Env ℓΔ} {κ : Kind ℓκ}{ι : Kind ℓι}
         → Type (Δ ، ι) κ → Type Δ ι → Type Δ κ

private
  variable
    Δ : Env ℓ
    κ κ' κ₁ κ₂ κ₃ : Kind ℓκ

data TVar where
  Z : TVar (Δ ، κ) κ
  S : TVar Δ κ₁ → TVar (Δ ، κ₂) κ₁
  Sₚ : ∀ {ℓκ'} {κ : Kind ℓκ} {κ' : Kind ℓκ'} → {Δ : Env ℓΔ} → {π : Pred Δ κ'}    →  TVar Δ κ → TVar (Δ ؛ π) κ

--------------------------------------------------------------------------------
-- Predicates.

data Pred where
  Wk-π  : ∀ {κ' : Kind ℓκ} {π : Pred Δ κ'} → Pred Δ κ → Pred (Δ ؛ π) κ
  Wk-κ  : ∀ {κ' : Kind ℓκ} → Pred Δ κ → Pred (Δ ، κ') κ

  _≲_ : ∀ {κ : Kind ι} →
          (ρ₁ : Type Δ R[ κ ]) →
          (ρ₂ : Type Δ R[ κ ]) →
          Pred Δ κ

  _·_~_ : ∀ {κ : Kind ι} →
            (ρ₁ : Type Δ R[ κ ]) →
            (ρ₂ : Type Δ R[ κ ]) →
            (ρ₃ : Type Δ R[ κ ]) →
            Pred Δ κ

-- -----------------------
-- -- Predicate variables.

data PVar where
  Z : ∀ {Δ : Env ℓΔ} {κ : Kind ℓκ} {π : Pred Δ κ} {π' : Pred (Δ ؛ π) κ} →
        PVar (Δ ؛ π) π'

  S : ∀ 
        {π : Pred Δ κ₁} {π' : Pred Δ κ₂} →
        PVar Δ π → PVar (Δ ؛ π') (Wk-π π)
  S-κ : ∀ 
        {π : Pred Δ κ₁} →
        PVar Δ π → PVar (Δ ، κ) (Wk-κ π)



data Type where

  ------------------------------------------------------------
  -- System Fω.

  tvar :

         TVar Δ κ →
         -----------
         Type Δ κ

  _`→_ :
          Type Δ (★ ℓ₁) → Type Δ (★ ℓ₂) →
          -----------------------------------
          Type Δ (★ (ℓ₁ ⊔ ℓ₂))

  `∀ :
          (κ : Kind ℓκ) → Type (Δ ، κ) (★ ℓ) →
          -------------------------------------
          Type Δ (★ (ℓ ⊔ (lsuc ℓκ)))

  `λ :
          (κ₁ : Kind ℓκ₁) → Type (Δ ، κ₁) κ₂ →
          -----------------------------------------
          Type Δ (κ₁ `→ κ₂)

  _·[_] :
          Type Δ (κ₁ `→ κ₂) → Type Δ κ₁ →
          -----------------------------
          Type Δ κ₂

  ------------------------------------------------------------
  -- Qualified types.

  _⇒_ : ∀ {κ : Kind ℓκ} →
         (π : Pred Δ κ) → Type (Δ ؛ π) (★ ℓ) →
         --------------------------------
         Type Δ (★ (lsuc ℓκ ⊔ ℓ))

  ------------------------------------------------------------
  -- System Rω.

  -- The empty row.
  ε : Type Δ R[ κ ]

  -- Row complement
  _─_ : 
        (ρ₂ : Type Δ R[ κ ]) → (ρ₁ : Type Δ R[ κ ]) →
        ---------------------------------------------
        Type Δ R[ κ ]


  -- Labels.
  lab :
        Label →
        ----------
        Type Δ (L ℓ)

  -- singleton formation.
  _▹_ :
        Type Δ (L ℓ) → Type Δ κ →
        -------------------
        Type Δ κ

  -- Row singleton formation.
  _R▹_ :
         Type Δ (L ℓ) → Type Δ κ →
         ---------------------------
         Type Δ R[ κ ]

  -- label constant formation.
  ⌊_⌋ :
        Type Δ (L ℓ) →
        ----------
        Type Δ (★ ℓ)

  -- Record formation.
  Π :
      Type Δ R[ κ ] →
      -------------
      Type Δ  κ

  -- Variant formation.
  Σ :
      Type Δ R[ κ ] →
      -------------
      Type Δ κ

  -- Lifting/mapping operations... I claim the kinds are at least
  -- self-evident now, even if the placement of arrows is a little bit
  -- arbitrary...

  ↑_ : {κ₁ : Kind ℓ₁} {κ₂ : Kind ℓ₂} →
       Type Δ R[ κ₁ `→ κ₂ ] →
       ------------------------------
       Type Δ (κ₁ `→ R[ κ₂ ])


  _↑ : {κ₁ : Kind ℓ₁} {κ₂ : Kind ℓ₂} →
       Type Δ (κ₁ `→ κ₂) →
       ------------------------------
       Type Δ (R[ κ₁ ] `→ R[ κ₂ ])

  ------------------------------------------------------------
  -- System Rωμ.

  -- μ formation.
  μ : ∀ {ℓ} →
      (τ : Type Δ ((★ ℓ) `→ (★ ℓ))) →
      -----------------------------------------------
      Type Δ (★ ℓ)

--------------------------------------------------------------------------------

data Ent where

  n-var : ∀ {π : Pred Δ κ} →
           PVar Δ π →
           -----------
           Ent Δ π

  n-refl : ∀  {τ : Type Δ R[ κ ]}  →

          --------------
          Ent Δ (τ ≲ τ)

  n-trans : ∀  {τ₁ τ₂ τ₃ : Type Δ R[ κ ]} →

          Ent Δ (τ₁ ≲ τ₂) → Ent Δ (τ₂ ≲ τ₃) →
          ---------------------------------------
          Ent Δ (τ₁ ≲ τ₃)

  n-≡ : ∀ {π₁ π₂ : Pred Δ κ} →

        π₁ ≡p π₂ → Ent Δ π₁ →
        ------------------------
        Ent Δ π₂

  n-≲lift₁ : ∀ {ρ₁ ρ₂ : Type Δ R[ κ₁ `→ κ₂ ]}
             {τ : Type Δ κ₁} →

             Ent Δ (ρ₁ ≲ ρ₂) →
             ---------------------
             Ent Δ ((↑ ρ₁ ·[ τ ]) ≲ (↑ ρ₂ ·[ τ ]))

  n-≲lift₂ : ∀ {ρ₁ ρ₂ : Type Δ R[ κ₁ ]}
               {ϕ : Type Δ (κ₁ `→ κ₂)} →

             Ent Δ (ρ₁ ≲ ρ₂) →
             ---------------------
             Ent Δ ((ϕ ↑ ·[ ρ₁ ]) ≲ (ϕ ↑ ·[ ρ₂ ]))

  n-·lift₁ : ∀ {ρ₁ ρ₂ ρ₃ : Type Δ R[ κ₁ `→ κ₂ ]}
               {τ : Type Δ κ₁} →

             Ent Δ (ρ₁ · ρ₂ ~ ρ₃) →
             ---------------------
             Ent Δ ((↑ ρ₁ ·[ τ ]) · (↑ ρ₂ ·[ τ ]) ~ (↑ ρ₃ ·[ τ ]))

  n-·lift₂ : ∀  {κ₁ : Kind ℓκ}
             {ρ₁ ρ₂ ρ₃ : Type Δ R[ κ₁ ]}
             {τ : Type Δ (κ₁ `→ κ₂)} →

             Ent Δ (ρ₁ · ρ₂ ~ ρ₃) →
             ---------------------
             Ent Δ ((τ ↑ ·[ ρ₁ ]) · (τ ↑ ·[ ρ₂ ]) ~ (τ ↑ ·[ ρ₃ ]))

  n-·≲L : ∀ {ρ₁ ρ₂ ρ₃ : Type Δ R[ κ ]} →

        Ent Δ (ρ₁ · ρ₂ ~ ρ₃) →
        ---------------------
        Ent Δ (ρ₁ ≲ ρ₃)


  n-·≲R : ∀ {ρ₁ ρ₂ ρ₃ : Type Δ R[ κ ]} →

        Ent Δ (ρ₁ · ρ₂ ~ ρ₃) →
        ---------------------
        Ent Δ (ρ₂ ≲ ρ₃)

  n-ε-R : ∀  {κ₁ : Kind ℓκ}
             {ρ : Type Δ R[ κ₁ ]} →

             -------------------------
             Ent Δ (ρ · ε ~ ρ)

  n-ε-L : ∀  {κ₁ : Kind ℓκ}
             {ρ : Type Δ R[ κ₁ ]} →

             -------------------------
             Ent Δ (ε · ρ ~ ρ)

--------------------------------------------------------------------------------
-- equivalence

data _≡p_ where
  peq-≲ : ∀ {τ₁ τ₂ υ₁ υ₂ : Type Δ R[ κ ]} →

          τ₁ ≡t υ₁ → τ₂ ≡t υ₂ →
          ------------------------
          (τ₁ ≲ τ₂) ≡p υ₁ ≲ υ₂

  peq-· : ∀ {τ₁ τ₂ τ₃ υ₁ υ₂ υ₃ : Type Δ R[ κ ]} →

            τ₁ ≡t υ₁ → τ₂ ≡t υ₂ → τ₃ ≡t υ₃ →
            ----------------------------------
            (τ₁ · τ₂ ~ τ₃) ≡p (υ₁ · υ₂ ~ υ₃)

data _≡t_ where
  teq-refl : {τ : Type Δ κ} →

             ---------------
             τ ≡t τ

  teq-sym : ∀ {τ₁ τ₂ : Type Δ κ} →

              τ₁ ≡t τ₂ →
              ---------------
              τ₂ ≡t τ₁

  teq-trans : ∀ {τ₁ τ₂ τ₃ : Type Δ κ} →

                τ₁ ≡t τ₂ → τ₂ ≡t τ₃ →
                ----------------------
                τ₁ ≡t τ₃

  teq-⇒ : ∀ {π₁ π₂ : Pred Δ κ} {τ₁ : Type (Δ ؛ π₁) (★ ℓ)} {τ₂ : Type (Δ ؛ π₂) (★ ℓ)} →

               π₁ ≡p π₂ → τ₁ ≡t τ₂ →
               -----------------------
               π₁ ⇒ τ₁ ≡t π₂ ⇒ τ₂

  teq-∀ : ∀ {τ υ : Type (Δ ، κ) (★ ℓ)} →

            τ ≡t υ →
            ----------------
            `∀ κ τ ≡t `∀ κ υ

  teq-β     : ∀ {κ' : Kind ℓκ} {τ : Type (Δ ، κ) κ'} {υ : Type Δ κ} →

                ------------------------------
                ((`λ κ τ) ·[ υ ]) ≡t (_β[_] τ υ)

  teq-· : ∀ {τ₁ υ₁ : Type Δ (κ `→ κ')} {τ₂ υ₂ : Type Δ κ} →

           τ₁ ≡t υ₁ → τ₂ ≡t υ₂ →
           ------------------------
           τ₁ ·[ τ₂ ] ≡t υ₁ ·[ υ₂ ]

  teq-sing : ∀  {l₁ l₂ : Type Δ (L ℓ)} →
                {τ₁ τ₂ : Type Δ κ₁} →

                l₁ ≡t l₂ → τ₁ ≡t τ₂ →
                --------------------------------------
                (l₁ R▹ τ₁) ≡t (l₂ R▹ τ₂)

  teq-lift₁ : ∀ {l : Type Δ (L ℓ)} {υ : Type Δ κ} {τ : Type Δ (κ `→ κ')} →

                --------------------------------------
                (↑ (l R▹ τ)) ·[ υ ] ≡t (l R▹ (τ ·[ υ ]))


  teq-lift₂ : ∀ {l : Type Δ (L ℓ)} {υ : Type Δ (κ₁ `→ κ₂)} {τ : Type Δ κ₁} →

                --------------------------------------
                (υ ↑) ·[ l R▹ τ ] ≡t (l R▹ (υ ·[ τ ]))

  teq-⌊⌋    : ∀ {τ υ : Type Δ (L ℓ)} →

                τ ≡t υ →
                -------------
                ⌊_⌋ {ℓ = ℓ} τ ≡t ⌊_⌋ {ℓ = ℓ} υ

  teq-Π : ∀ {ρ₁ ρ₂ : Type Δ R[ ★ ℓκ ] } →
          ρ₁ ≡t ρ₂ →
          -------------
          Π ρ₁ ≡t Π ρ₂

  teq-Σ : ∀ {ρ₁ ρ₂ : Type Δ R[ ★ ℓκ ] } →
          ρ₁ ≡t ρ₂ →
          -------------
          Σ ρ₁ ≡t Σ ρ₂

  teq-lift₃ : ∀ {ρ₁ : Type Δ R[ κ `→ ★ ℓκ ] } {τ : Type Δ κ} →

                ---------------------------
                Σ ρ₁ ·[ τ ] ≡t Σ ((↑ ρ₁) ·[ τ ])


-- ex : ∀ {ℓ} → Env ℓ
-- ex {ℓ} = 
--   (((ε 
--   ، R[ ★ ℓ ]) 
--   ∶ (lab "s")) 
--   ؛ (tvar (Sₜ Z)  ≲ tvar (Sₜ Z))) 

-- foo : ∀ {ℓ} → PVar (ex {ℓ}) (tvar (Sₚ (Sₜ Z)) ≲ tvar (Sₚ (Sₜ Z)))
-- foo = Z
